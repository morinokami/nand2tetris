// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array chars;
    field int maxLength;
    field int length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int _maxLength) {
        if (_maxLength < 1) {
            let _maxLength = 1;
        }

        let chars = Array.new(_maxLength);
        let length = 0;
        let maxLength = _maxLength;

        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do chars.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return chars[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let chars[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length < maxLength) {
            let chars[length] = c;
            let length = length + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length > 0) {
            let length = length - 1;
        }
        return;
    }

    function int charToInt(char c) {
        return c - 48;
    }

    function char digitToChar(int d) {
        return d + 48;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int val, d, i;
        var boolean positive;

        if (chars[0] = 45) { // '-'
            let positive = false;
            let i = 1;
        } else {
            let positive = true;
            let i = 0;
        }

        while (i < length) {
            let d = String.charToInt(chars[i]);
            let val = val * 10 + d;
            let i = i + 1;
        }

        if (positive) {
            return val;
        }
        return -val;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        let length = 0; // clear the string
        if (val < 0) {
            let val = -val;
            do appendChar(45); // append '-'
        }

        do setIntHelper(val);

        return;
    }

    method void setIntHelper(int val) {
        var int lastDigit;
        var char c;

        let lastDigit = val - ((val / 10) * 10);
        let c = String.digitToChar(lastDigit);
        if (val < 10) {
            do appendChar(c);
        } else {
            do setIntHelper(val / 10);
            do appendChar(c);
        }

        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
